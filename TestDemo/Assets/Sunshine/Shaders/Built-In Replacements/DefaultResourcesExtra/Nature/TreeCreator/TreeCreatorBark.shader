// Updated for Sunshine 1.4.3
Shader "Sunshine/Nature/Tree Creator Bark" {
Properties {
	_Color ("Main Color", Color) = (1,1,1,1)
	_Shininess ("Shininess", Range (0.01, 1)) = 0.078125
	_MainTex ("Base (RGB) Alpha (A)", 2D) = "white" {}
	_BumpMap ("Normalmap", 2D) = "bump" {}
	_GlossMap ("Gloss (A)", 2D) = "black" {}
	
	// These are here only to provide default values
	_SpecColor ("Specular Color", Color) = (0.5, 0.5, 0.5, 1)
	_Scale ("Scale", Vector) = (1,1,1,1)
	_SquashAmount ("Squash", Float) = 1
}

SubShader { 
	Tags { "RenderType"="TreeBark" }
	LOD 200
		
CGPROGRAM
#pragma exclude_renderers flash
#pragma glsl_no_auto_normalization

#include "Assets/Sunshine/Shaders/Compatibility/Helper.cginc"
#if UnityMajorVersion >= 5
	#include "Assets/Sunshine/Shaders/Compatibility/UnityBuiltin3xTreeLibrary.cginc"
#else
	#include "Tree.cginc"
#endif


 #include "Assets/Sunshine/Shaders/Sunshine.cginc"
#pragma multi_compile SUNSHINE_DISABLED SUNSHINE_FILTER_PCF_4x4 SUNSHINE_FILTER_PCF_3x3 SUNSHINE_FILTER_PCF_2x2 SUNSHINE_FILTER_HARD
#pragma target 3.0
#pragma surface surf BlinnPhong vertex:sunshine_surf_vert exclude_path:prepass


sampler2D _MainTex;
sampler2D _BumpMap;
sampler2D _GlossMap;
half _Shininess;

struct Input {
	float2 uv_MainTex;
	fixed4 color : COLOR;
	SUNSHINE_INPUT_PARAMS;
};

SUNSHINE_SURFACE_VERT_PIGGYBACK(Input, TreeVertBark)

void surf (Input IN, inout SurfaceOutput o) {
	fixed4 c = tex2D(_MainTex, IN.uv_MainTex);
	o.Albedo = c.rgb * _Color.rgb * IN.color.a;
	o.Gloss = SUNSHINE_SAMPLE_1CHANNEL(_GlossMap, IN.uv_MainTex);
	o.Alpha = c.a;
	o.Specular = _Shininess;
	o.Normal = UnpackNormal(tex2D(_BumpMap, IN.uv_MainTex));
}
ENDCG
}

Dependency "OptimizedShader" = "Hidden/Sunshine/Nature/Tree Creator Bark Optimized"
FallBack "Nature/Tree Creator Bark"
}
